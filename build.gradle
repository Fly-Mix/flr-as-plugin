plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.16'
}

sourceCompatibility = 1.8

// 插件需要支持的 IntelliJ IDEA Community Edition平台版本
// 平台版本使用其 branch_number 作为区分
// 关于`branch_number`的更多细节，可参考 https://www.jetbrains.org/intellij/sdk/docs/basics/getting_started/build_number_ranges.html
enum PlatformType {
    // Android Studio 3.5
    IC_191 ('191'),
    // Android Studio 3.6
    IC_192 ('192'),
    // Android Studio 4.0
    IC_193 ('193'),

    final String branchNumber

    PlatformType(String branchNumber) {
        this.branchNumber = branchNumber
    }

    String getBranchNumber() {
        return this.branchNumber
    }

    String toString(){
        'IC-' + branchNumber
    }
}

// flr-plugin-engine 的版本号
// 关于其介绍，可看文档《flr_plugin_workflow》（注：文档中使用的字段为 plugin_engine_version）
// 该值在完成一个新版本的 flr-plugin-engine 开发后，手动更新
String pluginEngineVersion = '3.1.0'

// 用于开发 flr-plugin-engine 时使用平台版本
// 一般使用最新平台版本
PlatformType pluginEnginePlatform = PlatformType.IC_193

// 用于开发 flr-plugin 时使用平台版本
// 该值在切换到 platform-master 分支后，手动更新
// 如切换到 191-master 分支后，该值更新为 PlatformType.IC_191
PlatformType pluginProductPlatform = pluginEnginePlatform

// flr-plugin 的版本号
// 关于其介绍，可看文档《flr_plugin_workflow》（注：文档中使用的字段为 plugin_product_version）
//
// plugin_product_version = #{IC_branch_number}.#{plugin_engine_version}
//
// example:
// IC_branch_number = 191
// plugin_engine_version = 1.0.0
// plugin_product_version = 191.1.0.0
//
String pluginProductVersion = pluginProductPlatform.branchNumber + '.' + pluginEngineVersion

// 打包文件的基本名称
// example: flr-as-plugin-191.1.0.0
archivesBaseName = rootProject.name + '-' + pluginProductVersion

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'org.yaml', name: 'snakeyaml', version: '1.26'
}

// See https://github.com/JetBrains/gradle-intellij-plugin#intellij-platform-properties
intellij {

    String platformVersion = null
    List platformPlugins = null

    switch (pluginProductPlatform) {
        // Config for Android Studio 3.5
        case PlatformType.IC_191:
            platformVersion = '2019.1.4'
            platformPlugins =  ['Dart:191.8593', 'io.flutter:43.0.1']
            break
        // Config for Android Studio 3.6
        case PlatformType.IC_192:
            platformVersion = '2019.2.4'
            platformPlugins = ['Dart:192.7761', 'io.flutter:43.0.2']
            break
        // Config for Android Studio 4.0
        case PlatformType.IC_193:
            platformVersion = '2019.3.3'
            platformPlugins = ['Dart:193.7361', 'io.flutter:45.1.2']
            break
    }

    assert(platformVersion != null && platformPlugins != null)

    version = platformVersion
    plugins = platformPlugins

    /*
    * Use this property if you want to test your plugin in any non-IDEA JetBrains IDE such as WebStorm or Android Studio.
    *
    * See https://github.com/JetBrains/gradle-intellij-plugin#rundebug-ide-properties
    * */
    //alternativeIdePath '/Applications/Android Studio.app'
}

// See https://github.com/JetBrains/gradle-intellij-plugin#patching-dsl
patchPluginXml {

    // 设置插件的版本号
    version = pluginProductVersion

    // 设置插件描述
    pluginDescription = """
<p>Flr (Flutter-R): A Flutter Resource Manager Android Studio Plugin, which can help flutter developer to auto specify assets in pubspec.yaml and generate r.g.dart file.</p>
<br>
<p>You can get more details from:</p>
<ul>
  <li><a href="https://github.com/Fly-Mix/flr-as-plugin">Flr GitHub Repository</a></li>
  <li><a href="https://plugins.jetbrains.com/plugin/13789-flr/">Flr Plugin Homepage</a></li>
</ul>
    """

    /*
    * 插件兼容IDEA的最小版本号和最大版本号
    * 详细可参考：
    * - https://github.com/JetBrains/gradle-intellij-plugin#patching-dsl
    * - http://www.jetbrains.org/intellij/sdk/docs/basics/getting_started/build_number_ranges.html
    * */
    sinceBuild = pluginProductPlatform.branchNumber
    //untilBuild = '191.*'

    /*
    * 设置版本更新日志
    * 注意：最新版本号的标题样式为h3，其他版本号的标题样式为h4
    */
    changeNotes = """
<h2>Flr Plugin Change Notes</h2>

<br/>
<h2>Flr Plugin Engine Change Notes</h2>
<h3>3.1.0</h3>
<ul>
  <li>Support for processing(init/generate/monitor) multi projects(the main project and its sub projects in one workspace)</li>
  <li>Support for auto merging old asset specifications when specifying new assets</li>
</ul>
<h4>3.0.0</h4>
<ul>
  <li>Support for processing non-implied resource file which is outside of "lib/" directory (such as "~/path/to/flutter_r_demo/assets/images/test.png")</li>
  <li>New recommended flutter resource structure</li>
</ul>
<h4>2.0.0</h4>
<ul>
  <li>New asset generation algorithm to support all kinds of standard or nonstandard image/text resource structure</li>
  <li>New asset-id generation algorithm to support assets with the same filename but different path</li>
  <li>New recommended flutter resource structure</li>
</ul>
<h4>1.1.0</h4>
<ul>
  <li>Improve generate-capability to support nonstandard image resource structure</li>
  <li>Add recommend-capability to display the recommended flutter resource structure</li>
</ul>
<h4>1.0.0</h4>
<ul>
  <li>Automatically specify assets in pubspec.yaml and generate r.g.dart file</li>
  <li>Support for processing image assets (.png, .jpg, .jpeg, .gif, .webp, .icon, .bmp, .wbmp, .svg)</li>
  <li>Support for processing image asset variants</li>
  <li>Support for processing text assets (.txt, .json, .yaml, .xml)</li>
  <li>Support for processing font assets (.ttf, .otf, .ttc)</li>
  <li>Improve robustness</li>
</ul>
    """
}